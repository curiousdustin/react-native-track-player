import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}

apply plugin: 'com.android.library'

boolean dash = false, hls = false, smoothstreaming = false

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    dash = json.dash ?: dash
    hls = json.hls ?: hls
    smoothstreaming = json.smoothstreaming ?: smoothstreaming
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 28)

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 16) // RN's minimum version
        targetSdkVersion safeExtGet("targetSdkVersion", 28)

        versionCode 1
        versionName "1.0"
        
        consumerProguardFiles 'proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "store"
    productFlavors {
        google {
            dimension "store"
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
        amazon {
            dimension "store"
            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
        }
    }
}

repositories {
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        // Make sure you have installed the react-native npm package before compiling
        url "../node_modules/react-native/android"
    }

    jcenter()
    google()
    mavenCentral()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'
    
    def exoPlayerVersion = safeExtGet("exoPlayerVersion", '2.10.1')

    // ExoPlayer Core
    googleImplementation "com.google.android.exoplayer:exoplayer-core:${exoPlayerVersion}"
    amazonImplementation "com.amazon.android:exoplayer-core:${exoPlayerVersion}"

    // ExoPlayer DASH
    if (dash) {
        googleImplementation "com.google.android.exoplayer:exoplayer-dash:${exoPlayerVersion}"
        amazonImplementation "com.amazon.android:exoplayer-dash:${exoPlayerVersion}"
    } else {
        googleCompileOnly "com.google.android.exoplayer:exoplayer-dash:${exoPlayerVersion}"
        amazonCompileOnly "com.amazon.android:exoplayer-dash:${exoPlayerVersion}"
    }

    // ExoPlayer HLS
    if (hls) {
        googleImplementation "com.google.android.exoplayer:exoplayer-hls:${exoPlayerVersion}"
        amazonImplementation "com.amazon.android:exoplayer-hls:${exoPlayerVersion}"
    } else {
        googleCompileOnly "com.google.android.exoplayer:exoplayer-hls:${exoPlayerVersion}"
        amazonCompileOnly "com.amazon.android:exoplayer-hls:${exoPlayerVersion}"
    }

    // ExoPlayer SmoothStreaming
    if (smoothstreaming) {
        googleImplementation "com.google.android.exoplayer:exoplayer-smoothstreaming:${exoPlayerVersion}"
        amazonImplementation "com.amazon.android:exoplayer-smoothstreaming:${exoPlayerVersion}"
    } else {
        googleCompileOnly "com.google.android.exoplayer:exoplayer-smoothstreaming:${exoPlayerVersion}"
        amazonCompileOnly "com.amazon.android:exoplayer-smoothstreaming:${exoPlayerVersion}"
    }

    // Make sure we're using androidx
    implementation "androidx.core:core:1.2.0-beta01"
    implementation "androidx.media:media:1.1.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0-alpha01"
    implementation "com.github.bumptech.glide:glide:4.7.1"
}
